plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

repositories {
	mavenCentral()
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

jar {
    manifest {
        attributes 'Implementation-Title': 'act-statemachine',
                   'Implementation-Version': archiveVersion
	}
}

dependencies {
	api(project(':act-core'))
	testImplementation(platform('org.junit:junit-bom:5.9.0'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation('org.junit.platform:junit-platform-suite-engine:1.9.0')
	testImplementation('org.assertj:assertj-core:3.23.1')
}

tasks.named('test') {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	archiveClassifier = 'sources'
}

artifacts {
	archives javadocJar, sourcesJar
}

if (project.hasProperty("signing.keyId")) {
	apply plugin: 'signing'

	signing {
	    sign configurations.archives
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				pom{
					name = 'act-statemachine'
					description = 'A language for state charts.'
					url = 'http://requirementsascode.org/'
					packaging = 'jar'

					from components.java

      				artifact javadocJar
					artifact sourcesJar

					licenses {
						license {
							name = 'The Apache Software License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id = 'bertilmuth'
							name = 'Bertil Muth'
						}
					}

					scm {
						url = 'scm:git@github.com:bertilmuth/requirementsascode.git'
						connection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
						developerConnection = 'scm:git@github.com:bertilmuth/requirementsascode.git'
					}
				}
			}
		}

		repositories {
			maven {
				credentials {
					username sonatypeUsername
					password sonatypePassword
				}

				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}